[script]
n0=/**
n1=* Connect.ini
n2=*
n3=* Handle joining networks
n4=*
n5=* Dependencies:
n6=* Misc.ini (expand_vars)
n7=* 
n8=* Global variables: 
n9=*   %<$network>_username
n10=*   %<$network>_password
n11=*   %<$network>_favs
n12=*   %<$network>_login
n13=*   %<$network>_hide_ip
n14=*/
n15=
n16=
n17=/*
n18=* Aliases
n19=*/
n20=
n21=/**
n22=* <network> <varname> [value]
n23=*/
n24=alias set_var {
n25=  if ($3 == $null) unset % [ $+ [ $$1 ] $+ _ $+ [ $$2 ] ]
n26=  else % [ $+ [ $$1 ] $+ _ $+ [ $$2 ] ] = $3-
n27=}
n28=
n29=alias get_var {
n30=  return % [ $+ [ $$1 ] $+ _ $+ [ $$2 ] ]
n31=}
n32=
n33=alias set_username {
n34=  $set_var($1, username, $2)
n35=}
n36=
n37=alias get_username {
n38=  return $get_var($1, username)
n39=}
n40=
n41=alias set_password {
n42=  $set_var($1, password, $2)
n43=}
n44=
n45=alias get_password {
n46=  return $get_var($1, password)
n47=}
n48=
n49=alias set_login {
n50=  $set_var($1, login, $2-)
n51=}
n52=
n53=alias get_login {
n54=  return $get_var($1, login)
n55=}
n56=
n57=alias set_hide_ip {
n58=  $set_var($1, hide_ip, $2-)
n59=}
n60=
n61=alias get_hide_ip {
n62=  return $get_var($1, hide_ip)
n63=}
n64=
n65=/*
n66=* Favorites (auto-joined channels) will have the format:
n67=* <#channel> [password] [args]
n68=* 
n69=* Limited to 100 entries (0-99)
n70=*/
n71=
n72=alias set_fav {
n73=  $set_var($1, fav $+ $$2, $3-)
n74=}
n75=
n76=alias get_fav {
n77=  return $get_var($1, fav $+ $$2)
n78=}
n79=
n80=alias add_favs {
n81=  var %i = 0
n82=  var %j = 2
n83=  while ((%j <= $0) && (%i < 100)) {
n84=    if ($get_fav($1, %i) == $null) {
n85=      $set_fav($1, %i, $ [ $+ [ %j ] ] )
n86=      inc %j
n87=    }
n88=    inc %i
n89=  }
n90=}
n91=
n92=alias get_fav_id {
n93=  var %i = 0
n94=  var %network = $1
n95=  var %chan = $2
n96=  if ((%network == $null) || (%chan == $null)) return
n97=  while (%i < 100) {
n98=    ; Split at spaces
n99=    tokenize 32 $get_fav(%network, %i)
n100=    if ($1 == %chan) break
n101=    inc %i
n102=  }
n103=  if (%i == 100) return
n104=  return %i
n105=}
n106=
n107=alias is_fav {
n108=  if ($get_fav_id($1, $2) != $null) return $true
n109=  return $false
n110=}
n111=
n112=alias toggle_fav {
n113=  var %id = $get_fav_id($1, $2)
n114=  if (%id == $null) $add_favs($1, $2)
n115=  else $set_fav($1, %id)
n116=}
n117=
n118=alias join_favs {
n119=  var %i = 0
n120=  var %network = $1
n121=  if (%network == $null) return
n122=  while (%i < 100) {
n123=    tokenize 32 $get_fav(%network, %i)
n124=    timer 1 10 join $3 $$1 $2
n125=    inc %i
n126=  }
n127=}
n128=
n129=alias login {
n130=  $$get_login($1) $$get_username($1) $$get_password($1)
n131=}
n132=
n133=alias hide_ip {
n134=  $$expand_vars($$get_hide_ip($1))
n135=}
n136=
n137=
n138=/*
n139=* Events 
n140=*/
n141=
n142=on 1:connect: {
n143=  login $network
n144=  hide_ip $network
n145=  join_favs $network
n146=}
n147=
n148=
n149=/*
n150=* Menus
n151=*/
n152=menu channel {
n153=  -  
n154=  $iif($is_fav($network, $chan), $style(1)) Auto-join: $toggle_fav($network, $chan)
n155=  -
n156=}
n157=
n158=menu status {
n159=  -
n160=  Set Username: $set_username($network, $$?="Enter your username for this network")
n161=  Set Password: $set_password($network, $$?*="Enter your password for this network")
n162=  -
n163=}
