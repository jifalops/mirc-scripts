[script]
n0=/*
n1=* Event handlers
n2=*/
n3=
n4=on 1:start: {
n5=  %ar_data_file = AutoRespondData.ini
n6=  if(!$isfile(%ar_data_file)) $ar_initialize()
n7=}
n8=
n9=; Check the auto responder for a match every time 
n10=; someone enters text into a channel or a PM
n11=on 1:text:*:*: {
n12=  ar_check $nick $1-
n13=}
n14=
n15=
n16=/*
n17=* Menus
n18=*/
n19=
n20=menu channel {
n21=  -
n22=  Global Responders
n23=  . Add...: $ar_add_responder(.global, $$?="Enter wildcard text to look for (* for any):", $$?="Enter a response or command.")
n24=  . $iif(!$ar_get_value(.global, responders), $style(2), $iif($ar_is_paused(.global), $style(1))) Pause: $ar_toggle_pause(.global)
n25=  . $ar_get_responder(.global, 1): $ar_del_responder(.global, 1)
n26=  . $ar_get_responder(.global, 2): $ar_del_responder(.global, 2)
n27=  . $ar_get_responder(.global, 3): $ar_del_responder(.global, 3)
n28=  . $ar_get_responder(.global, 4): $ar_del_responder(.global, 4)
n29=  . $ar_get_responder(.global, 5): $ar_del_responder(.global, 5)
n30=  . $ar_get_responder(.global, 6): $ar_del_responder(.global, 6)
n31=  . $ar_get_responder(.global, 7): $ar_del_responder(.global, 7)
n32=  . $ar_get_responder(.global, 8): $ar_del_responder(.global, 8)
n33=  . $ar_get_responder(.global, 9): $ar_del_responder(.global, 9)
n34=  . $ar_get_responder(.global, 10): $ar_del_responder(.global, 10)
n35=  . $iif(!$ar_get_value(.global, triggered), $style(2)) Triggered $ar_get_triggered(.global): $iif($$?="Enter 'y' to reset." == y, $ar_set_value(.global, triggered, 0))
n36=  . $iif($ar_get_value(.global, responders) == $null, $style(2)) Clear: $ar_del_host(.global)
n37=  Show All: $ar_show_all()
n38=  $iif(!$ar_get_value(.system, hosts), $style(2), $iif($ar_is_paused(.system), $style(1))) Pause All: $ar_toggle_pause(.system)
n39=  $iif($ar_get_value(.system, triggered) == 0, $style(2)) Triggered $ar_get_triggered(.system): $iif($$?="Enter 'y' to reset." == y, $ar_set_value(.system, triggered, 0))
n40=  Clear All...: $iif($$?="Enter 'y' to confirm." == y, $ar_initialize())
n41=  -
n42=}
n43=menu nicklist {
n44=  -
n45=  Auto-respond
n46=  . Add...: $ar_add_responder($$1, $$?="Enter wildcard text to look for (* for any):", $$?="Enter a response or command.")
n47=  . $iif($ar_is_mimiced($$1), $style(1)) Mimic: $ar_toggle_mimic($$1)  
n48=  . $iif(!$ar_get_value($$1, responders), $style(2), $iif($ar_is_paused($$1), $style(1))) Pause: $ar_toggle_pause($$1)
n49=  . $ar_get_responder($$1, 1): $ar_del_responder($$1, 1)
n50=  . $ar_get_responder($$1, 2): $ar_del_responder($$1, 2)
n51=  . $ar_get_responder($$1, 3): $ar_del_responder($$1, 3)
n52=  . $ar_get_responder($$1, 4): $ar_del_responder($$1, 4)
n53=  . $ar_get_responder($$1, 5): $ar_del_responder($$1, 5)
n54=  . $iif(!$ar_get_value($$1, triggered), $style(2)) Triggered $ar_get_triggered($$1): $iif($$?="Enter 'y' to reset." == y, $ar_set_value($$1, triggered, 0))
n55=  . $iif($ar_get_value($$1, responders) == $null, $style(2)) Clear: $ar_del_host($$1)  
n56=  Show All: $ar_show_all()
n57=  $iif(!$ar_get_value(.system, hosts), $style(2), $iif($ar_is_paused(.system), $style(1))) Pause All: $ar_toggle_pause(.system)
n58=  $iif($ar_get_value(.system, triggered) == 0, $style(2)) Triggered $ar_get_triggered(.system): $iif($$?="Enter 'y' to reset." == y, $ar_set_value(.system, triggered, 0))
n59=  Clear All...: $iif($$?="Enter 'y' to confirm." == y, $ar_initialize())
n60=  -
n61=}
n62=
n63=
n64=/*
n65=* Aliases 
n66=*/
n67=
n68=; Reset all variables and hash tables
n69=alias ar_initialize { 
n70=  write -c %ar_data_file
n71=  $ar_set_value(.system, hosts, 0)
n72=  $ar_set_value(.system, responders, 0)
n73=  $ar_set_value(.system, triggered, 0)
n74=  $ar_set_value(.system, pause, false)
n75=  write -a %ar_data_file $lf
n76=}
n77=
n78=
n79=; Get a user's hostname
n80=; Accepts a nick, a hostname, ".global" or ".system"
n81=; Might not return a value!
n82=alias ar_get_host {
n83=  if (. !isin $$1) var %host = $ial($$1).host  
n84=  else var %host = $$1
n85=  if (%host == $null) userhost $$1 
n86=  return %host
n87=}
n88=
n89=
n90=; Get a user's current nick name from their hostname
n91=; Accepts a nick, a hostname, ".global", or ".system"
n92=; Might not return a value!
n93=alias ar_get_nick {
n94=  if ($$1 == .global) return
n95=  if ($$1 == .system) return
n96=  if (. isin $$1) {
n97=    var %mask = *!*@ $+ [ $1 ]
n98=    var %nick = $ial(%mask).nick      
n99=  }
n100=  else var %nick = $$1
n101=  return %nick
n102=}
n103=
n104=
n105=; user
n106=alias ar_add_host {
n107=  write -a %ar_data_file $lf
n108=  if ($$1 != .global) $ar_set_value($$1, last_nick, $ar_get_nick($$1))
n109=  $ar_set_value($$1, responders, 0)
n110=  $ar_set_value($$1, triggered, 0)
n111=  $ar_set_value($$1, pause, false)
n112=  if ($$1 != .global) $ar_set_value($$1, mimic, false)
n113=
n114=  $ar_inc_value(.system, hosts)
n115=}
n116=
n117=; user
n118=alias ar_del_host {
n119=  remini %ar_data_file $$1
n120=  $ar_dec_value(.system, hosts)
n121=}
n122=
n123=; user, item, value
n124=alias ar_set_value {
n125=  writeini -n %ar_data_file $$1 $$2 $$3
n126=}
n127=
n128=; user, item
n129=alias ar_get_value {
n130=  return $readini(%ar_data_file, n, $$1, $$2)
n131=}
n132=
n133=alias ar_get_command {
n134=  return $readini(%ar_data_file, p, $$1, $$2)
n135=}
n136=
n137=; user, item
n138=alias ar_del_value {
n139=  remini %ar_data_file $$1 $$2
n140=}
n141=
n142=
n143=
n144=; user, item
n145=alias ar_inc_value {
n146=  var %value = $ar_get_value($$1, $$2)
n147=  inc %value
n148=  $ar_set_value($$1, $$2, %value)
n149=}
n150=
n151=
n152=; user, item
n153=alias ar_dec_value {
n154=  var %value = $ar_get_value($$1, $$2)
n155=  dec %value
n156=  $ar_set_value($$1, $$2, %value)
n157=}
n158=
n159=
n160=; Add a listen/response pair for a user
n161=alias ar_add_responder {
n162=  var %host = $ar_get_host($$1)
n163=  if (%host == $null) return
n164=
n165=  if ($ar_get_value(%host, responders) == $null) {
n166=    $ar_add_host(%host)
n167=    var %index = 1
n168=  }
n169=  else var %index = $calc($ar_get_value(%host, responders) + 1)
n170=  $ar_set_value(%host, listen $+ [ %index ] , $$2)
n171=  $ar_set_value(%host, response $+ [ %index ] , $$3)
n172=  $ar_set_value(%host, responders, %index)
n173=
n174=  $ar_inc_value(.system, responders)
n175=}
n176=
n177=
n178=; Retrieve a listen/response pair for a user
n179=; The second paramater is the index of the pair
n180=; to look up (1, 2, 3...)
n181=alias ar_get_responder {
n182=  var %host = $ar_get_host($1)
n183=  if (%host == $null) return 
n184=
n185=  var %index = $$2 
n186=  var %listen = $ar_get_value(%host, listen $+ [ %index ] )
n187=  var %response = $ar_get_value(%host, response $+ [ %index ] )
n188=  if (%listen && %response) { return %listen => %response }
n189=}
n190=
n191=; user, index
n192=alias ar_del_responder {
n193=  var %host = $ar_get_host($$1)
n194=  if (%host == $null) return
n195=
n196=  $ar_del_value(%host, listen $+ [ $$2 ] )
n197=  $ar_del_value(%host, response $+ [ $$2 ] )
n198=
n199=  $ar_dec_value(%host, responders)
n200=  $ar_dec_value(.system, responders)  
n201=}
n202=
n203=
n204=alias ar_show_all {
n205=  var %sections = $ini(%ar_data_file, 0)
n206=  var %section
n207=  var %items
n208=  var %item
n209=  var %value
n210=  var %i = 1
n211=  var %j  
n212=  while (%i <= %sections) {
n213=    %section = $ini(%ar_data_file, %i)
n214=
n215=    ; Output a table header.
n216=    echo 1 $str(-, $calc($len(%section) + 4))
n217=    echo 1 * %section *
n218=    echo 1 $str(-, $calc($len(%section) + 4))
n219=
n220=    ; Echo each item-value pair.
n221=    %items = $ini(%ar_data_file, %i, 0)
n222=    %j = 1
n223=    while (%j <= %items) {
n224=      %item = $ini(%ar_data_file, %i, %j)
n225=      %value = $ar_get_value(%section, %item)
n226=      echo 1 %item = %value
n227=      inc %j
n228=    }
n229=    echo 1 -
n230=    inc %i
n231=  }
n232=}
n233=
n234=
n235=; this is called on a text event
n236=; expects a nick and the text to check
n237=alias ar_check {
n238=  if ($ar_get_value(.system, pause) == true) return
n239=  var %host = $ar_get_host($$1)
n240=  if ($ar_get_value(%host, pause) == false) $ar_check_host(%host, $$2-)
n241=  if ($ar_get_value(.global, pause) == false) $ar_check_host(.global, $$2-)
n242=}
n243=
n244=
n245=; Checks the specified table for responders.
n246=; host, msg-
n247=alias ar_check_host {  
n248=  var %host = $$1
n249=  var %msg = $$2-
n250=  var %responders = $ar_get_value(%host, responders)
n251=  var %listen
n252=  var %response
n253=  var %i = 1
n254=
n255=  if (%host != .global) $ar_set_value(%host, last_nick, $ar_get_nick(%host))
n256=  if ($ar_get_value(%host, mimic) == true) { 
n257=    msg $chan %msg
n258=    $ar_inc_value(%host, triggered)
n259=    $ar_inc_value(.system, triggered)
n260=  }
n261=
n262=  while (%i <= %responders) {
n263=    %listen = $ar_get_value(%host, listen $+ [ %i ] )
n264=    if (%listen iswm %msg) {
n265=      %response = $ar_get_value(%host, response $+ [ %i ] )   
n266=      if ($left(%response, 1) == $chr(47)) {
n267=        %response = $ar_get_command(%host, response $+ [ %i ] )
n268=        ;%response
n269=      }
n270=      elseif ($chan) msg $chan %response
n271=      else msg $nick %response
n272=
n273=      $ar_inc_value(%host, triggered)
n274=      $ar_inc_value(.system, triggered)
n275=    }
n276=    inc %i
n277=  }
n278=}
n279=
n280=
n281=; Handles toggling of mimic for a user
n282=alias ar_toggle_mimic {
n283=  var %host = $ar_get_host($$1)
n284=  if (%host == $null) return 
n285=
n286=  if ($ar_get_value(%host, responders) == $null) {
n287=    $ar_add_host(%host)
n288=    $ar_set_value(%host, mimic, true)
n289=  }
n290=  else {
n291=    if ($ar_get_value(%host, mimic) == true) {
n292=      if($ar_get_value(%host, responders) == 0) $ar_del_host(%host)
n293=      else $ar_set_value(%host, mimic, false)     
n294=    }
n295=    else $ar_set_value(%host, mimic, true)
n296=  }
n297=}
n298=
n299=
n300=; Handles toggling of pause for a user
n301=alias ar_toggle_pause {
n302=  var %host = $ar_get_host($$1)
n303=  if (%host == $null) return 
n304=
n305=  if ($ar_get_value(%host, pause) == true) $ar_set_value(%host, pause, false)
n306=  else $ar_set_value(%host, pause, true)
n307=}
n308=
n309=
n310=; checks if mimic is enabled for a user
n311=alias ar_is_mimiced {
n312=  var %host = $ar_get_host($$1)
n313=  if (%host == $null) return 
n314=  if ($ar_get_value(%host, mimic) == true) return $true
n315=}
n316=
n317=
n318=; checks if a user is paused
n319=alias ar_is_paused {
n320=  var %host = $ar_get_host($$1)  
n321=  if (%host == $null) return 
n322=  if ($ar_get_value(%host, pause) == true) return $true
n323=}
n324=
n325=
n326=; gets the number of times auto responders have been
n327=; triggered for a user (since last cleared).
n328=alias ar_get_triggered {
n329=  var %host = $ar_get_host($$1)
n330=  if (%host == $null) return 
n331=  var %t = $ar_get_value(%host, triggered)
n332=  if (!%t) %t = 0
n333=  return %t
n334=}
