[script]
n0=/*
n1=* Event handlers
n2=*/
n3=
n4=on 1:start: {
n5=  %ar_data_file = AutoRespondData.ini
n6=  if (!$isfile(%ar_data_file)) $ar_initialize()
n7=}
n8=
n9=; Check the auto responder for a match every time 
n10=; someone enters text into a channel or a PM
n11=on 1:text:*:*: {
n12=  ar_check $nick $1-
n13=}
n14=
n15=
n16=/*
n17=* Menus
n18=*/
n19=
n20=menu channel {
n21=  -
n22=  $style(2) Global Auto-Responder (broken)
n23=  .Global Responders
n24=  .. Add...: $ar_add_responder(.global, $$?="Enter wildcard text to look for (* for any):", $$?="Enter a response or command.")
n25=  .. $iif(!$ar_get_value(.global, responders), $style(2), $iif($ar_is_paused(.global), $style(1))) Pause: $ar_toggle_pause(.global)
n26=  .. $ar_get_responder(.global, 1): $ar_del_responder(.global, 1)
n27=  .. $ar_get_responder(.global, 2): $ar_del_responder(.global, 2)
n28=  .. $ar_get_responder(.global, 3): $ar_del_responder(.global, 3)
n29=  .. $ar_get_responder(.global, 4): $ar_del_responder(.global, 4)
n30=  .. $ar_get_responder(.global, 5): $ar_del_responder(.global, 5)
n31=  .. $ar_get_responder(.global, 6): $ar_del_responder(.global, 6)
n32=  .. $ar_get_responder(.global, 7): $ar_del_responder(.global, 7)
n33=  .. $ar_get_responder(.global, 8): $ar_del_responder(.global, 8)
n34=  .. $ar_get_responder(.global, 9): $ar_del_responder(.global, 9)
n35=  .. $ar_get_responder(.global, 10): $ar_del_responder(.global, 10)
n36=  .. $iif(!$ar_get_value(.global, triggered), $style(2)) Triggered $ar_get_triggered(.global): $iif($$?="Enter 'y' to reset." == y, $ar_set_value(.global, triggered, 0))
n37=  .. $iif($ar_get_value(.global, responders) == $null, $style(2)) Clear: $ar_del_host(.global)
n38=  .Show All: $ar_show_all()
n39=  .$iif(!$ar_get_value(.system, hosts), $style(2), $iif($ar_is_paused(.system), $style(1))) Pause All: $ar_toggle_pause(.system)
n40=  .$iif($ar_get_value(.system, triggered) == 0, $style(2)) Triggered $ar_get_triggered(.system): $iif($$?="Enter 'y' to reset." == y, $ar_set_value(.system, triggered, 0))
n41=  .Clear All...: $iif($$?="Enter 'y' to confirm." == y, $ar_initialize())
n42=  -
n43=}
n44=menu nicklist {
n45=  -
n46=  $style(2) Auto-Responder (broken)
n47=  .Auto-respond
n48=  .. Add...: $ar_add_responder($$1, $$?="Enter wildcard text to look for (* for any):", $$?="Enter a response or command.")
n49=  .. $iif($ar_is_mimiced($$1), $style(1)) Mimic: $ar_toggle_mimic($$1)  
n50=  .. $iif(!$ar_get_value($$1, responders), $style(2), $iif($ar_is_paused($$1), $style(1))) Pause: $ar_toggle_pause($$1)
n51=  .. $ar_get_responder($$1, 1): $ar_del_responder($$1, 1)
n52=  .. $ar_get_responder($$1, 2): $ar_del_responder($$1, 2)
n53=  .. $ar_get_responder($$1, 3): $ar_del_responder($$1, 3)
n54=  .. $ar_get_responder($$1, 4): $ar_del_responder($$1, 4)
n55=  .. $ar_get_responder($$1, 5): $ar_del_responder($$1, 5)
n56=  .. $iif(!$ar_get_value($$1, triggered), $style(2)) Triggered $ar_get_triggered($$1): $iif($$?="Enter 'y' to reset." == y, $ar_set_value($$1, triggered, 0))
n57=  .. $iif($ar_get_value($$1, responders) == $null, $style(2)) Clear: $ar_del_host($$1)  
n58=  .Show All: $ar_show_all()
n59=  .$iif(!$ar_get_value(.system, hosts), $style(2), $iif($ar_is_paused(.system), $style(1))) Pause All: $ar_toggle_pause(.system)
n60=  .$iif($ar_get_value(.system, triggered) == 0, $style(2)) Triggered $ar_get_triggered(.system): $iif($$?="Enter 'y' to reset." == y, $ar_set_value(.system, triggered, 0))
n61=  .Clear All...: $iif($$?="Enter 'y' to confirm." == y, $ar_initialize())
n62=  -
n63=}
n64=
n65=
n66=/*
n67=* Aliases 
n68=*/
n69=
n70=; Reset all variables and hash tables
n71=alias ar_initialize { 
n72=  write -c %ar_data_file
n73=  $ar_set_value(.system, hosts, 0)
n74=  $ar_set_value(.system, responders, 0)
n75=  $ar_set_value(.system, triggered, 0)
n76=  $ar_set_value(.system, pause, false)
n77=  write -a %ar_data_file $lf
n78=}
n79=
n80=
n81=; Get a user's hostname
n82=; Accepts a nick, a hostname, ".global" or ".system"
n83=; Might not return a value!
n84=alias ar_get_host {
n85=  if (. !isin $$1) var %host = $ial($$1).host  
n86=  else var %host = $$1
n87=  if (%host == $null) userhost $$1 
n88=  return %host
n89=}
n90=
n91=
n92=; Get a user's current nick name from their hostname
n93=; Accepts a nick, a hostname, ".global", or ".system"
n94=; Might not return a value!
n95=alias ar_get_nick {
n96=  if ($$1 == .global) return
n97=  if ($$1 == .system) return
n98=  if (. isin $$1) {
n99=    var %mask = *!*@ $+ [ $1 ]
n100=    var %nick = $ial(%mask).nick      
n101=  }
n102=  else var %nick = $$1
n103=  return %nick
n104=}
n105=
n106=
n107=; user
n108=alias ar_add_host {
n109=  write -a %ar_data_file $lf
n110=  if ($$1 != .global) $ar_set_value($$1, last_nick, $ar_get_nick($$1))
n111=  $ar_set_value($$1, responders, 0)
n112=  $ar_set_value($$1, triggered, 0)
n113=  $ar_set_value($$1, pause, false)
n114=  if ($$1 != .global) $ar_set_value($$1, mimic, false)
n115=
n116=  $ar_inc_value(.system, hosts)
n117=}
n118=
n119=; user
n120=alias ar_del_host {
n121=  remini %ar_data_file $$1
n122=  $ar_dec_value(.system, hosts)
n123=}
n124=
n125=; user, item, value
n126=alias ar_set_value {
n127=  writeini -n %ar_data_file $$1 $$2 $$3
n128=}
n129=
n130=; user, item
n131=alias ar_get_value {
n132=  return $readini(%ar_data_file, n, $$1, $$2)
n133=}
n134=
n135=alias ar_get_command {
n136=  return $readini(%ar_data_file, p, $$1, $$2)
n137=}
n138=
n139=; user, item
n140=alias ar_del_value {
n141=  remini %ar_data_file $$1 $$2
n142=}
n143=
n144=
n145=
n146=; user, item
n147=alias ar_inc_value {
n148=  var %value = $ar_get_value($$1, $$2)
n149=  inc %value
n150=  $ar_set_value($$1, $$2, %value)
n151=}
n152=
n153=
n154=; user, item
n155=alias ar_dec_value {
n156=  var %value = $ar_get_value($$1, $$2)
n157=  dec %value
n158=  $ar_set_value($$1, $$2, %value)
n159=}
n160=
n161=
n162=; Add a listen/response pair for a user
n163=alias ar_add_responder {
n164=  var %host = $ar_get_host($$1)
n165=  if (%host == $null) return
n166=
n167=  if ($ar_get_value(%host, responders) == $null) {
n168=    $ar_add_host(%host)
n169=    var %index = 1
n170=  }
n171=  else var %index = $calc($ar_get_value(%host, responders) + 1)
n172=  $ar_set_value(%host, listen $+ [ %index ] , $$2)
n173=  $ar_set_value(%host, response $+ [ %index ] , $$3)
n174=  $ar_set_value(%host, responders, %index)
n175=
n176=  $ar_inc_value(.system, responders)
n177=}
n178=
n179=
n180=; Retrieve a listen/response pair for a user
n181=; The second paramater is the index of the pair
n182=; to look up (1, 2, 3...)
n183=alias ar_get_responder {
n184=  var %host = $ar_get_host($1)
n185=  if (%host == $null) return 
n186=
n187=  var %index = $$2 
n188=  var %listen = $ar_get_value(%host, listen $+ [ %index ] )
n189=  var %response = $ar_get_value(%host, response $+ [ %index ] )
n190=  if (%listen && %response) { return %listen => %response }
n191=}
n192=
n193=; user, index
n194=alias ar_del_responder {
n195=  var %host = $ar_get_host($$1)
n196=  if (%host == $null) return
n197=
n198=  $ar_del_value(%host, listen $+ [ $$2 ] )
n199=  $ar_del_value(%host, response $+ [ $$2 ] )
n200=
n201=  $ar_dec_value(%host, responders)
n202=  $ar_dec_value(.system, responders)  
n203=}
n204=
n205=
n206=alias ar_show_all {
n207=  var %sections = $ini(%ar_data_file, 0)
n208=  var %section
n209=  var %items
n210=  var %item
n211=  var %value
n212=  var %i = 1
n213=  var %j  
n214=  while (%i <= %sections) {
n215=    %section = $ini(%ar_data_file, %i)
n216=
n217=    ; Output a table header.
n218=    echo 1 $str(-, $calc($len(%section) + 4))
n219=    echo 1 * %section *
n220=    echo 1 $str(-, $calc($len(%section) + 4))
n221=
n222=    ; Echo each item-value pair.
n223=    %items = $ini(%ar_data_file, %i, 0)
n224=    %j = 1
n225=    while (%j <= %items) {
n226=      %item = $ini(%ar_data_file, %i, %j)
n227=      %value = $ar_get_value(%section, %item)
n228=      echo 1 %item = %value
n229=      inc %j
n230=    }
n231=    echo 1 -
n232=    inc %i
n233=  }
n234=}
n235=
n236=
n237=; this is called on a text event
n238=; expects a nick and the text to check
n239=alias ar_check {
n240=  if ($ar_get_value(.system, pause) == true) return
n241=  var %host = $ar_get_host($$1)
n242=  if ($ar_get_value(%host, pause) == false) $ar_check_host(%host, $$2-)
n243=  if ($ar_get_value(.global, pause) == false) $ar_check_host(.global, $$2-)
n244=}
n245=
n246=
n247=; Checks the specified table for responders.
n248=; host, msg-
n249=alias ar_check_host {  
n250=  var %host = $$1
n251=  var %msg = $$2-
n252=  var %responders = $ar_get_value(%host, responders)
n253=  var %listen
n254=  var %response
n255=  var %i = 1
n256=
n257=  if (%host != .global) $ar_set_value(%host, last_nick, $ar_get_nick(%host))
n258=  if ($ar_get_value(%host, mimic) == true) { 
n259=    msg $chan %msg
n260=    $ar_inc_value(%host, triggered)
n261=    $ar_inc_value(.system, triggered)
n262=  }
n263=
n264=  while (%i <= %responders) {
n265=    %listen = $ar_get_value(%host, listen $+ [ %i ] )
n266=    if (%listen iswm %msg) {
n267=      %response = $ar_get_value(%host, response $+ [ %i ] )   
n268=      if ($left(%response, 1) == $chr(47)) {
n269=        %response = $ar_get_command(%host, response $+ [ %i ] )
n270=        ;%response
n271=      }
n272=      elseif ($chan) msg $chan %response
n273=      else msg $nick %response
n274=
n275=      $ar_inc_value(%host, triggered)
n276=      $ar_inc_value(.system, triggered)
n277=    }
n278=    inc %i
n279=  }
n280=}
n281=
n282=
n283=; Handles toggling of mimic for a user
n284=alias ar_toggle_mimic {
n285=  var %host = $ar_get_host($$1)
n286=  if (%host == $null) return 
n287=
n288=  if ($ar_get_value(%host, responders) == $null) {
n289=    $ar_add_host(%host)
n290=    $ar_set_value(%host, mimic, true)
n291=  }
n292=  else {
n293=    if ($ar_get_value(%host, mimic) == true) {
n294=      if($ar_get_value(%host, responders) == 0) $ar_del_host(%host)
n295=      else $ar_set_value(%host, mimic, false)     
n296=    }
n297=    else $ar_set_value(%host, mimic, true)
n298=  }
n299=}
n300=
n301=
n302=; Handles toggling of pause for a user
n303=alias ar_toggle_pause {
n304=  var %host = $ar_get_host($$1)
n305=  if (%host == $null) return 
n306=
n307=  if ($ar_get_value(%host, pause) == true) $ar_set_value(%host, pause, false)
n308=  else $ar_set_value(%host, pause, true)
n309=}
n310=
n311=
n312=; checks if mimic is enabled for a user
n313=alias ar_is_mimiced {
n314=  var %host = $ar_get_host($$1)
n315=  if (%host == $null) return 
n316=  if ($ar_get_value(%host, mimic) == true) return $true
n317=}
n318=
n319=
n320=; checks if a user is paused
n321=alias ar_is_paused {
n322=  var %host = $ar_get_host($$1)  
n323=  if (%host == $null) return 
n324=  if ($ar_get_value(%host, pause) == true) return $true
n325=}
n326=
n327=
n328=; gets the number of times auto responders have been
n329=; triggered for a user (since last cleared).
n330=alias ar_get_triggered {
n331=  var %host = $ar_get_host($$1)
n332=  if (%host == $null) return 
n333=  var %t = $ar_get_value(%host, triggered)
n334=  if (!%t) %t = 0
n335=  return %t
n336=}
